configfile: "config.json"

SAMPLES, = glob_wildcards(config['haplohiv_folder']+"ec/{id}.fastq")

rule all:
    input:        
        expand("{prefix}/haplotypes/{samples}_haplotypes.fa", prefix=config['output_dir'], samples=SAMPLES)

#Map error corrected reads to the masterconsensus
rule map_to_consensus:
    input:
        reads = config['haplohiv_folder']+"ec/{samples}.fastq",
        consensus = config['output_dir']+"/consensus/masterconsensus_"+config['patient']+"_consensus.fa"
    output:
        config['output_dir']+"/mapped/{samples}.sam"
    shell:
        """
        bwa mem -p {input.consensus} {input.reads} > {output}
        """

#Configure PredictHaplo settings
rule configure_predicthaplo:
    input:
        dummy = config['output_dir']+"/PredictHaplo.conf",
        sample = "{prefix}/mapped/{samples}.sam",
        true_haplotypes = "{prefix}/consensus/masterconsensus_"+config['patient']+"_aligned.fa"
    output:
        "{prefix}/predicthaplo/{samples}.conf"
    shell:
        """
        cp {input.dummy} {output}
        sed -i "s|prefix_line|{config[output_dir]}/predicthaplo/output_{wildcards.samples}/{wildcards.samples}|" {output}
        sed -i "s|reference_line|{config[output_dir]}/consensus/masterconsensus_{config[patient]}_consensus.fa|" {output}
        sed -i "s|reads_line|{input.sample}|" {output}
        sed -i "s|true_haplotypes_line|{input.true_haplotypes}|" {output}
        """

#Run PredictHaplo
rule predicthaplo:
    input:
        "{prefix}/predicthaplo/{samples}.conf"
    output:
        "{prefix}/predicthaplo/output_{samples}/{samples}.conf"
    shell:
        """
        PredictHaplo-Paired {input}
        mv {input} {output}
        """
        
#Retrieve haplotype sequences and frequencies from the PredictHaplo output
rule gather_haplotype:
    input:
        "{prefix}/predicthaplo/output_{samples}/{samples}.conf"
    output:
        "{prefix}/haplotypes/{samples}_haplotypes.fa"
    shell:
        """
        mkdir -p {config[output_dir]}/haplotypes
        python scripts/gather_haplotypes.py {config[output_dir]}/predicthaplo/output_{wildcards.samples}/{wildcards.samples}global_*.fas \
        {output} {wildcards.samples}
        """
